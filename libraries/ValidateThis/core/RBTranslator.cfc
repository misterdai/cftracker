<!---
	
	Copyright 2009, Bob Silverberg
	
	Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in 
	compliance with the License.  You may obtain a copy of the License at 
	
		http://www.apache.org/licenses/LICENSE-2.0
	
	Unless required by applicable law or agreed to in writing, software distributed under the License is 
	distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or 
	implied.  See the License for the specific language governing permissions and limitations under the 
	License.
	
--->
<cfcomponent output="false" name="RBTranslator" extends="BaseTranslator" hint="I use Resource Bundles to translate text.">

	<cffunction name="translate" returnType="any" access="public" output="false" hint="I translate text">
		<cfargument name="translateThis" type="Any" required="true" />
		<cfargument name="locale" type="Any" required="false" default="" />
		
		<cfset var theKey = safeKey(arguments.translateThis) />

		<cfif arguments.locale EQ variables.defaultLocale AND arguments.translateThis NEQ theKey>
			<!--- This allows a user to not have to provide translations for the default error messages generated by the framework for their default language --->
			<cfreturn arguments.translateThis />
		<cfelse>
			<cfif NOT StructKeyExists(variables.instance.locales,arguments.locale)>
				<cfthrow type="validatethis.core.RBTranslator.LocaleNotDefined"
					message="The locale requested, '#arguments.locale#', has not been defined in the localeMap in the ValidateThisConfig." />
			<cfelseif NOT StructKeyExists(variables.instance.locales[arguments.locale],theKey)>
				<cfthrow type="validatethis.core.RBTranslator.KeyNotDefined"
					message="The key requested, '#theKey#', has not been defined in the locale '#arguments.locale#'." />
			</cfif>
			<cfreturn variables.instance.locales[arguments.locale][theKey] />
		</cfif>
	</cffunction>

	<cffunction name="safeKey" returnType="any" access="public" output="false" hint="I take a message and turn it into a key">
		<cfargument name="message" type="Any" required="true" />
		
		<cfreturn REReplace(Replace(arguments.message," ","_","all"),"\W","","all") />
	</cffunction>

</cfcomponent>
	

